apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'

defaultTasks 'clean', 'build'

group = 'org.neptunepowered'
version = '1.0-SNAPSHOT'
archivesBaseName = project.name.toLowerCase()

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
    mavenCentral()
    maven {
        name = 'Visual Illusions Repository'
        url = 'http://repo.visualillusionsent.net/repository/public/'
    }
    maven {
        url "https://libraries.minecraft.net"
    }
    maven {
        name = 'mcstats.org releases'
        url = 'http://repo.mcstats.org/content/repositories/releases/'
    }
    maven {
        name = 'mcstats.org snapshots'
        url = 'http://repo.mcstats.org/content/repositories/snapshots/'
    }
    maven {
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile 'net.canarymod:CanaryLib:1.2.1-SNAPSHOT'
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: System.getenv().DEPLOY_ROOT + "/maven_repo")

            pom {
                groupId = project.group
                artifactId = project.archivesBaseName
                version = project.version

                project {
                    name project.archivesBaseName
                    description "Extensions to Canarylib"
                    packaging 'jar'
                    url "http://www.neptunepowered.org/"

                    scm {
                        url "https://github.com/NeptunePowered/NeptuneLib"
                        connection "scm:git:https://github.com/NeptunePowered/NeptuneLib.git"
                        developerConnection "scm:git:git@github.com:NeptunePowered/NeptuneLib.git"
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
